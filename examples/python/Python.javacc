/*
 * This Python grammar corresponds to the information on the 
 * following two web pages:
 * https://docs.python.org/3/reference/grammar.html
 * https://docs.python.org/3/reference/lexical_analysis.html#indentation
 * This is Python 3.9.6, which I presume is the latest spec. 
 * (I do not anticipate supporting any older versions of the language.)
 * Currently, the generated parser can parse the source code of Django
 * and the Python standard library, with the exception of a few files
 * that are deliberately screwy.
 */

PARSER_PACKAGE = org.parsers.python;
NODE_PACKAGE = org.parsers.python.ast;
DEFAULT_LEXICAL_STATE = PYTHON;
TABS_TO_SPACES=8;
ENSURE_FINAL_EOL;
PRESERVE_LINE_ENDINGS=false;
DEACTIVATE_TOKENS=MATCH,CASE,UNDERSCORE;
//DEBUG_LEXER;
//DEBUG_PARSER;

INCLUDE "PythonLexer.javacc" 

PythonFile : (Statement)* <EOF> ;

Number#void : <HEXNUMBER> | <BINNUMBER> | <OCTNUMBER> | <DECNUMBER> | <FLOAT> | <COMPLEX> ;

RealNumber#void :
  <HEXNUMBER> | <BINNUMBER> | <OCTNUMBER> | <DECNUMBER> | <FLOAT>
;  

SignedNumber : 
  ["-"]
  Number
  [("+"|"-") <COMPLEX>]
;

Strings : (<STRING_LITERAL>)+ ;

AtomicExpression#void : 
    <NAME> 
    | 
    Number 
    | 
    Strings
    | 
    "True" 
    | 
    "False" 
    | 
    "None" 
    | 
    "__peg_parser__" 
    |
    (Tuple | GenExp | Group)
    |
    (ListComp | PyList)
    |
    (SetComp | DictComp | PyDict | PySet)
    |
    "..."
;

Slice :
    [Expression] ":" =>|| [Expression] [":" [Expression]]
    |
    Expression
;

Slices : "[" Slice ("," =>|+1 Slice)* [","] "]" ;

Statement : 
   SCAN MatchStatementLA =>
   MatchStatement 
   | 
   SimpleStatement 
   | 
   CompoundStatement 
;

SimpleStatement#(>2) : SmallStatement (";" =>|+1 SmallStatement)* [";"] <NEWLINE> ; 
           
SmallStatement : 
   Assignment
   |
   StarExpressions
   |
   ReturnStatement
   |
   ImportStatement 
   |
   GlobalStatement
   |
   NonlocalStatement
   |
   "break"
   |
   "continue"
   |
   "pass"
   |
   DelStatement
   |
   AssertStatement
   |
   RaiseStatement
   |
   YieldStatement
;

CompoundStatement :
    FunctionDefinition
    |
    IfStatement
    |
    ClassDefinition
    |
    WithStatement
    |
    WhileStatement
    |
    ForStatement
    |
    TryStatement
;

// This production is only used 
// for lookahead
MatchStatementLA :
    <NAME>
    [
        SCAN {!getToken(0).getImage().equals("match")}#
        => FAIL
    ]
    SubjectExpression ":"
;    
     

ReturnStatement : "return" [StarExpressions] ;

GlobalStatement : "global" <NAME> ("," <NAME>)* ;

NonlocalStatement : "nonlocal" <NAME> ("," <NAME>)* ;

AssertStatement : "assert" Expression ("," Expression)* ;

RaiseStatement : "raise" [ Expression ["from" Expression ] ];

Block :
    SimpleStatement
    |
    <NEWLINE><INDENT>(Statement)+<DEDENT>
;    

ElseBlock : "else" ":" Block ;

ExceptBlock : "except" [ Expression ["as" <NAME>] ] ":" Block ;

IfStatement : 
    "if" NamedExpression ":" Block 
     ("elif" NamedExpression ":" Block)* 
     ["else" ":" Block]
;     

FinallyBlock : "finally" ":" Block ;

WhileStatement : "while" NamedExpression ":" Block [ElseBlock] ;

WithStatement : 
    ["async"] "with" =>||
    (
        WithItem ("," WithItem)* 
        |
        "(" WithItem (SCAN 2 => "," WithItem)*  [","] ")"
    )
    ":" Block
;

WithItem : Expression ["as" StarTarget] ;


ForStatement : 
   ["async"] "for" =>|| 
   StarTargets "in" StarExpressions ":"
   Block [ElseBlock]
;

TryStatement : 
   "try" ":" Block 
   (
       FinallyBlock
       | 
       ((ExceptBlock)+ [ElseBlock] [FinallyBlock])
   )
;

DelStatement : "del" PrimaryExpression (SCAN 2 "," PrimaryExpression)* [","] ; 

NamedExpression : [ => <NAME> ":=" ] Expression ;

DottedName : <NAME> (=> "." <NAME>)* ;

ImportStatement : ImportName | ImportFrom1 | ImportFrom2 ;

ImportName#void : "import" DottedName ["as" <NAME>] ("," DottedName ["as" <NAME>])* ;

ImportFrom1#void : "from" ("." | "...")* DottedName =>|| "import"  ImportFromTargets ;

ImportFrom2#void : "from" ("." | "...")+ "import" =>|| ImportFromTargets ; 

ImportFromTargets : 
   "*"
   |
   <NAME> ["as" <NAME>] ("," <NAME> =>|| ["as" <NAME>])* [","]
   |
   "(" <NAME> ["as" <NAME>] ("," <NAME> =>|| ["as" <NAME>])* [","] ")" [","]
;

StarNamedExpression : 
     "*" BitwiseOr
     |
     NamedExpression 
;

StarExpression :
    "*" BitwiseOr
    |
    Expression
;

StarExpressions : StarExpression ("," =>|+1 StarExpression)* [","] ;

StarNamedExpressions : StarNamedExpression ("," =>|+1 StarNamedExpression)* [","] ;

BitwiseOr : BitwiseXor ("|" BitwiseXor)* ;

BitwiseXor : BitwiseAnd ("^" BitwiseAnd)* ;

BitwiseAnd : ShiftExpression ("&" ShiftExpression)* ;

ShiftExpression : AdditiveExpression (("<<" | ">>") AdditiveExpression)* ;

AdditiveExpression : MultiplicativeExpression (("+"|"-") MultiplicativeExpression)* ;

MultiplicativeExpression : UnaryExpression (("*"|"/"|"//"|"%"|"@") UnaryExpression)* ;

UnaryExpression : Power | ("+" | "-" | "~") UnaryExpression ;

Power : AwaitPrimary ["**" UnaryExpression] ;

Tuple : 
   SCAN "(" (")" | StarNamedExpression ",") =>
    "(" [StarNamedExpression "," [StarNamedExpressions]] ")" 
;

Group : "(" (YieldExpression | NamedExpression) ")" ;

AwaitPrimary : ["await"] PrimaryExpression ;

PrimaryExpression#void :
   AtomicExpression
   (
       "." <NAME> #DotName(3)
       |
       Slices #SliceExpression(2)
       |
       GenExp #GenExpression(2)
       |
       InvocationArguments #FunctionCall(2)
   )*
;

DotName : "." <NAME> ;

YieldExpression : 
    "yield" "from" =>|| Expression 
    |
    "yield" [StarExpressions]
;    

YieldStatement# : YieldExpression ;

AnnotatedRhs : YieldExpression | StarExpressions ;

Decorators : ("@" NamedExpression <NEWLINE>)+ ;

Annotation : ":" Expression ;

Default : "=" Expression ;

InvocationArguments : "(" [Args] ")" ;

Args : // This is looser than the official grammar, but okay for now...
   Arg (SCAN 3 "," Arg)*
   [","] 
;

Arg : 
   <NAME> (":=" | "=") =>|| Expression
   |
   ["*" | "**"] Expression
;

KeyValuePair : Expression ":" Expression ;

DoubleStarredKeyValuePair : 
    "**" BitwiseOr
    |
    KeyValuePair
;

DoubleStarredKeyValuePairs : DoubleStarredKeyValuePair ("," =>|+1 DoubleStarredKeyValuePair)* [","] ;

PyDict : 
  SCAN "{" ("}" | DoubleStarredKeyValuePair) =>
  (
    "{" 
    [DoubleStarredKeyValuePairs] 
    "}" 
  )
;

DictComp : "{" KeyValuePair =>|+2 (ForIfClause)+ "}" ;

PySet : "{" StarNamedExpressions "}" ;

SetComp : "{" NamedExpression =>|+2 (ForIfClause)+ "}" ;

ClassDefinition :
    [Decorators] "class" =>||
    <NAME> [InvocationArguments] 
    ":"
    Block
;

FunctionDefinition :
    [Decorators]
    ["async"]
    "def" =>||
    <NAME>
    "("
    Parameters
    ")"
    ["->" Expression]
    ":" Block
;

LambdaDefinition : "lambda" Parameters ":" Expression ;

/*
 * Expression grammar follows
 */

Expression : Disjunction ["if" Disjunction "else" Expression] | LambdaDefinition ; 

Disjunction : Conjunction ("or" Conjunction)* ;

Conjunction : Inversion ("and" Inversion)* ;

Inversion : ("not")* Comparison ;

Comparison : BitwiseOr (ComparisonOperator BitwiseOr)* ;

ComparisonOperator#void :
   "<" | ">" | "==" | ">=" | "<=" | "!=" 
   | 
   ["not"] "in" 
   |
   "is" ["not"]
;


StarTarget : ["*"] PrimaryExpression ;

StarTargets : StarTarget ("," =>|+1 StarTarget)* [","];

ForIfClause : ["async"] "for" StarTargets "in" Disjunction ("if" Disjunction)* ;

GenExp : "(" NamedExpression =>|+2 (ForIfClause)+ ")" ;

PyList : "[" [StarNamedExpressions] "]" ;

ListComp : "[" NamedExpression =>|+2 (ForIfClause)+ "]" ;

StarEtc :
   KeyWords
   |
   "*" "," =>|| ParamMaybeDefault ("," =>|+1 ParamMaybeDefault)* ["," [KeyWords]]
   |
   "*" <NAME> [SCAN 1 \.\FunctionDefinition => ":" Expression] ("," =>|+1 ParamMaybeDefault)* ["," [KeyWords]]
;

ParamMaybeDefault : <NAME> [":" Expression] ["=" Expression] ;

KeyWords :
   "**" <NAME> 
   [ 
       SCAN 1 ~\...\LambdaDefinition => 
       ":" Expression
   ]
;

Parameters : 
// Using a contextual predicate, we manage to use the same production 
// for the parameters of a function def or a lambda. 
// NB. This production is looser than it should be. For example,
// a default argument can only be at the end and the slash 
// can only appear once. FIXME.
    [
      <NAME> [SCAN 1 \FunctionDefinition => ":" Expression] ["=" Expression]  
      ("," =>|+1
        (
          "/"
          | 
          <NAME> [SCAN 1 \FunctionDefinition => ":" Expression] ["=" Expression] 
        )
      )* 
      [ "," ]
    ]
    [
        SCAN ~(")"|":") => StarEtc
    ]
    [","]
;

AugAssign#void :
    "+=" | "-=" | "*=" | "@=" | "/=" | "%=" | "&=" | "|=" | "^=" | "<<=" 
    | ">>=" | "**=" | "//=" 
;

SimpleAssignTarget :
   PrimaryExpression
   |
   "(" SimpleAssignTarget ")"
;

Assignment :
 SCAN (SimpleAssignTarget (":" | AugAssign)) | (StarTargets "=") =>
 (
  SimpleAssignTarget ":" =>|| Expression ["=" AnnotatedRhs]
  |
  SimpleAssignTarget AugAssign =>|| (YieldExpression | StarExpressions)
  |
  (=>StarTargets "=")+ =>|| (YieldExpression | StarExpressions)
 )
;

// Below this point, this is the new 3.10 pattern matching stuff
// At this moment, this is quite untested!

MatchStatement :
   ACTIVATE_TOKENS MATCH ("match") 
   SubjectExpression
   ":" <NEWLINE> <INDENT>
   (CaseBlock)+
   <DEDENT>
;

CaseBlock :
    SCAN 0 {getToken(1).getImage().equals("case")}#
    =>
    ACTIVATE_TOKENS CASE ("case")
    ACTIVATE_TOKENS UNDERSCORE (Patterns)
    ["if" NamedExpression]
    ":"
    Block
;


Patterns :
   OpenSequencePattern
   |
   Pattern
;

CapturePattern :
    SCAN 0 {getToken(1).getType()== NAME && getToken(2).getType() != DOT && getToken(2).getType() != LPAREN && getToken(2).getType()!= ASSIGN}
    =>
    <NAME>
;    

StarPattern :
   "*"
   (<UNDERSCORE> | CapturePattern)
;

MaybeStarPattern : StarPattern | Pattern ;

ClosedPattern :
   ClassPattern 
   |
   LiteralPattern 
   |
   CapturePattern
   |
   <UNDERSCORE>
   |
   ValuePattern
   |
   =>GroupPattern
   |
   SequencePattern
   |
   MappingPattern
;

ValuePattern :
   Attribute
   [
       SCAN {getToken(1).getType() == DOT || getToken(1).getType() == LPAREN || getToken(1).getType() == ASSIGN}#
       => FAIL
   ]
   =>||
;   

GroupPattern : "(" Pattern ")" ;

KeyValuePattern: (LiteralPattern | Attribute) ":" Pattern ;

ItemsPattern : KeyValuePattern ("," =>|+1 KeyValuePattern)* ;

MappingPattern :
  "{"
     [
         ItemsPattern ["," =>|+1 DoubleStarPattern] [","]
         |
         DoubleStarPattern [","]
     ]
  "}"
;

DoubleStarPattern : "**" CapturePattern ;

LiteralPattern :
   "None" 
   | 
   "True" 
   | 
   "False"
   |
   SignedNumber
   |
   Strings
;

MaybeSequencePattern :
   MaybeStarPattern ("," =>|+1 MaybeStarPattern)* [","]
;

OpenSequencePattern :
   MaybeStarPattern "," =>|| [MaybeSequencePattern]
;

SequencePattern :
    "[" [MaybeSequencePattern] "]"
    |
    "(" [OpenSequencePattern] ")"
;    

ClassPattern :
   NameOrAttribute
   "("
   =>||
      [
          SCAN <NAME> "=" =>
          KeywordPatterns [","]
          |
          PositionalPatterns ["," =>|+1 KeywordPatterns] [","]
      ]
   ")"
;

PositionalPatterns : 
   Pattern 
   (
       // KLUDGE!!!!
       SCAN 0 {getToken(1).getType()==COMMA && !( getToken(2).getType()==NAME && getToken(3).getType()==ASSIGN)}#
       => "," Pattern
   )* 
;

KeywordPattern : <NAME> "=" Pattern ;

KeywordPatterns : KeywordPattern ("," =>|+2 KeywordPattern)* ;

OrPattern : ClosedPattern ("|" ClosedPattern)* ;

Pattern : OrPattern ["as" CapturePattern] ;

SubjectExpression : 
   StarNamedExpression "," =>|| [StarNamedExpressions]
   |
   NamedExpression
;

Attribute : <NAME> ("." <NAME>)+ ;

NameOrAttribute #Attribute(>1) : <NAME> ("." <NAME>)* ;
