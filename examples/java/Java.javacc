/* Copyright (c) 2008-2021 Jonathan Revusky, revusky@javacc.com
 * Copyright (c) 2006, Sun Microsystems Inc.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *     * Redistributions of source code must retain the above copyright notices,
 *       this list of conditions and the following disclaimer.
 *     * Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *     * Neither the name Jonathan Revusky, Sun Microsystems, Inc.
 *       nor the names of any contributors may be used to endorse or promote
 *       products derived from this software without specific prior written
 *       permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF
 * THE POSSIBILITY OF SUCH DAMAGE.
 */

 // This is the Java grammar that is used (via an INCLUDE) in the JavaCC 21 
 // tool itself but it can be used on its own, and you are free to use it
 // in your own projects. It supports the stable feature set of the Java 
 // language up to JDK 15. 
 // This grammar is a forward evolution of the sample Java grammar originally 
 // included in the legacy JavaCC distribution in 1997 or thereabouts, but is
 // close to being a total rewrite, and serves as a kind of showcase for the latest
 // features in JavaCC 21, so many constructs are expressed in a very clean
 // and elegant way that is simply not possible with the legacy tool.

JAVA_UNICODE_ESCAPE;
PARSER_PACKAGE="org.parsers.java";
NODE_PACKAGE="org.parsers.java.ast";
DEFAULT_LEXICAL_STATE=JAVA;
PRESERVE_LINE_ENDINGS=false;
TABS_TO_SPACES=8;
//JDK_TARGET=15;

FAULT_TOLERANT;
DEBUG_FAULT_TOLERANT;

INCLUDE("JavaLexer.javacc")

INJECT PARSER_CLASS : {

  // The logic of the following token hook routine is explained here: 
  // https://javacc.com/2020/10/20/context-sensitive-tokenization/

  private Token TOKEN_HOOK(Token tok) {
    TokenType type = tok.getType();
    if (type == RSIGNEDSHIFT || type == RUNSIGNEDSHIFT) {
      if (isInProduction("TypeArguments", "TypeParameters")) {
        // If we've entered the TypeParameters or TypeArguments production, we need to split
        // a ">>" or ">>>" into 2 (or 3) GT tokens.
        Token gt = Token.split(tok, 1, GT, GT);
        if (type == RUNSIGNEDSHIFT) {
          Token next = Token.split(gt.getNext(), 1, GT, GT);
          gt.setNext(next);
          gt.setNextToken(next);
        }
        return gt;
      }
    }
    else if (type == GT) {
      Token next = tok.getNextToken();
      if (next != null && next.getType() == GT && !isInProduction("TypeArguments", "TypeParameters")) {
      // In this case we do the reverse. We merge 2 (or 3) GT tokens into a right shift operator
        Token nextNext = next.getNextToken();
        Token merged = Token.merge(tok, next, RSIGNEDSHIFT);
        if (nextNext != null && nextNext.getType() == GT) {
            merged = Token.merge(merged, nextNext, RUNSIGNEDSHIFT);
        } 
        return merged;
      }
    }
    return tok;
  }
  
  private Token TOKEN_HOOK(Token tok) {
    TokenType type = tok.getType();
    TokenType desiredType = type;
    String image = tok.getImage();
    if (type == RECORD && !inTypeDeclaration()) {
      desiredType = IDENTIFIER;
    }
    else if (type == IDENTIFIER && inTypeDeclaration()) {
      if (image.equals("record")) desiredType = RECORD;
      // I guess a var or a yield in a type declaration can just be treated as an invalid token, no? NO!!!
//      else if (image.equals("yield") || image.equals("var")) desiredType = INVALID;
    }
    if (type == desiredType) return tok;
    Token result = Token.newToken(desiredType, image, tok.getInputSource());
    result.copyLocationInfo(tok);
    return result;
  }

  private boolean inTypeDeclaration() {
    String currentProduction = currentLookaheadProduction == null ? currentlyParsedProduction : currentLookaheadProduction;
    // We can use reference equality because the strings are all interned.
    return currentProduction == "TypeDeclaration" 
           || currentProduction == "AnnotationTypeDeclaration"
           || currentProduction == "ClassDeclaration"
           || currentProduction == "InterfaceDeclaration"
           || currentProduction == "RecordDeclaration"
           || currentProduction == "EnumDeclaration"; 
  }
}

#CompilationUnit# :
  [ PackageDeclaration! ]
  ( ImportDeclaration )*!
  ( TypeDeclaration )+!
  [<EOF>]
  {
    return CURRENT_NODE;
  }
;

PackageDeclaration : (Annotation)* "package" =>|| Name ";" ;

INJECT PackageDeclaration :
{
  public String getPackageName() {
    Node nameNode = getChild(getChildCount()-2);
    return nameNode.toString();
  }
}

ImportDeclaration : "import" [ "static" ] Name [ "." "*" ] ";" ;

Modifiers(EnumSet<TokenType> permissibleModifiers) #void :
 (
  SCAN {isParserTolerant() 
       || permissibleModifiers.contains(getToken(1).getType())}
  =>
  (
   "public"|"static"|"protected"|"private"|"final"|"abstract" 
   |"synchronized"|"native"|"transient"|"volatile"|"strictfp" 
   |"default"
  )
  {
    switch(lastConsumedToken.getType()) {
        case ABSTRACT :
        case FINAL :
           permissibleModifiers.remove(ABSTRACT);
           permissibleModifiers.remove(FINAL);
           break;
        case PRIVATE :
        case PROTECTED :
        case PUBLIC :
           permissibleModifiers.remove(PRIVATE);
           permissibleModifiers.remove(PROTECTED);
           permissibleModifiers.remove(PUBLIC);
           break;
        default : 
           permissibleModifiers.remove(lastConsumedToken.getType());
    }
  }
  |
  Annotation
 )*
;

TypeDeclaration #abstract :
  SCAN ";" | (Modifiers ("class" | "enum" | "interface" | "record" | "@" "interface")) =>
  (
    EmptyDeclaration 
    |
    AnnotationTypeDeclaration
    |
    ClassDeclaration
    |
    InterfaceDeclaration
    |
    EnumDeclaration
    |
    RecordDeclaration
  )
;

INJECT EmptyDeclaration : extends TypeDeclaration;
INJECT AnnotationTypeDeclaration : extends TypeDeclaration;
INJECT ClassDeclaration : extends TypeDeclaration;
INJECT InterfaceDeclaration : extends TypeDeclaration;
INJECT EnumDeclaration : extends TypeDeclaration;

EmptyDeclaration# : ";" ;

ClassDeclaration :
  Modifiers(EnumSet.of(PUBLIC, PROTECTED, PRIVATE, ABSTRACT, FINAL, STATIC, STRICTFP))
  "class" =>|| 
  <IDENTIFIER>
  [ TypeParameters ]
  [ ExtendsList]
  [ ImplementsList ]
  ClassOrInterfaceBody
;

InterfaceDeclaration :
  Modifiers(EnumSet.of(PUBLIC, PROTECTED, PRIVATE, ABSTRACT, STATIC, STRICTFP))
  "interface" =>||
  <IDENTIFIER> 
  [ TypeParameters ]
  [ ExtendsList ] 
  ClassOrInterfaceBody
;

ExtendsList : 
   "extends" ObjectType
   ( 
     SCAN 1 \InterfaceDeclaration => 
     "," ObjectType 
   )*
;

INJECT ExtendsList :
import java.util.List; 
{
    public List<ObjectType> getTypes() {
        return childrenOfType(ObjectType.class);
    }
}

ImplementsList :
   "implements" ObjectType
   ( 
     "," ObjectType
   )*
;


EnumDeclaration :
   Modifiers(EnumSet.of(PUBLIC, PROTECTED, PRIVATE, ABSTRACT, FINAL, STATIC, STRICTFP))
   "enum" =>|| <IDENTIFIER>
   [ImplementsList] 
   EnumBody
;

EnumBody :
   "{"
   [ EnumConstant ( SCAN 2 "," EnumConstant )* ]
   [ "," ]
   [ ";" ( ClassOrInterfaceBodyDeclaration )*! ]
   "}"
;

RecordDeclaration :
  Modifiers(EnumSet.of(PUBLIC, PROTECTED, PRIVATE, ABSTRACT, FINAL, STATIC, STRICTFP))
  "record" =>||
  <IDENTIFIER>
  [TypeParameters]
  RecordHeader
  [ImplementsList]
  RecordBody
;

RecordHeader :
   "("
   [
     RecordComponent
     ("," RecordComponent)*
   ]
   ")"
;

RecordComponent :
   (Annotation)* Type [(Annotation)* "..."] <IDENTIFIER>
;

RecordBody :
  "{"
  (
   CompactConstructorDeclaration
   |
   ClassOrInterfaceBodyDeclaration
  )*
  "}"
;

CompactConstructorDeclaration :
  Modifiers(EnumSet.of(PUBLIC, PROTECTED, PRIVATE))
  <IDENTIFIER>
  "{"
  [ => ExplicitConstructorInvocation ]
  ( BlockStatement )*!
  "}" 
;

EnumConstant :
  (Annotation)* <IDENTIFIER> [ InvocationArguments ] [ ClassOrInterfaceBody ]
;

TypeParameters :
   "<"
   TypeParameter ( "," TypeParameter )* 
   ">"
;

INJECT TypeParameters : 
  import java.util.List;
{
    public List<TypeParameter> getParameters() {
        return childrenOfType(TypeParameter.class);
    }
}

TypeParameter: <IDENTIFIER> [TypeBound];

TypeBound :
   "extends" ObjectType ( "&" ObjectType )*
;

ClassOrInterfaceBody : "{" (ClassOrInterfaceBodyDeclaration)*! "}" ;

ClassOrInterfaceBodyDeclaration# :
  Initializer
  |
  TypeDeclaration
  |
  ConstructorDeclaration
  |
  FieldDeclaration
  |
  MethodDeclaration
;

FieldDeclaration :
  Modifiers(EnumSet.of(PUBLIC, PROTECTED, PRIVATE, STATIC, FINAL, TRANSIENT, VOLATILE))
  Type VariableDeclarator =>|+1 ( "," VariableDeclarator )* ";"
;

VariableDeclarator : VariableDeclaratorId [ "=" VariableInitializer ] ;

VariableDeclaratorId : <IDENTIFIER> ( "[" "]" )* ;

VariableInitializer : ArrayInitializer | Expression ;

ArrayInitializer :
  "{" [ VariableInitializer ( SCAN 2 "," VariableInitializer )* ] [ "," ] "}"
;

#MethodDeclaration :
  (
    SCAN \.\.\InterfaceDeclaration
    =>Modifiers(EnumSet.of(PUBLIC,PRIVATE,ABSTRACT,STATIC,SYNCHRONIZED,NATIVE,STRICTFP,_DEFAULT))
    |
    Modifiers(EnumSet.of(PUBLIC,PROTECTED,PRIVATE,ABSTRACT,STATIC,FINAL,SYNCHRONIZED,NATIVE,STRICTFP))
  )
  [ TypeParameters ]
  ReturnType
  <IDENTIFIER> 
  =>|+1 FormalParameters ( "[" "]" )*
  [ ThrowsList ]
  ( Block | ";" )
  {return CURRENT_NODE;}
;

FormalParameters : "(" [ FormalParameter ( "," FormalParameter )* ] ")" ;

FormalParameter : Modifiers(EnumSet.of(FINAL)) Type [ "..." ] VariableDeclaratorId ;

INJECT ConstructorDeclaration : extends ClassOrInterfaceBodyDeclaration

ConstructorDeclaration :
  Modifiers(EnumSet.of(PUBLIC, PROTECTED, PRIVATE))
  [ TypeParameters ]
  <IDENTIFIER> FormalParameters =>||
  [ ThrowsList ]
  "{"
  [ => ExplicitConstructorInvocation ]
  ( BlockStatement )*!
  "}"
;

ExplicitConstructorInvocation :
  [TypeArguments] ("this" | "super") =>|+1 InvocationArguments ";"
  |
  [=>Name "."] [TypeArguments] "super" =>|+1 InvocationArguments ";"
;

Initializer :
  [ "static" ] =>|+1 Block
;

Type :
   => ReferenceType
   |
   PrimitiveType
;

INJECT ReferenceType : extends Type;
INJECT PrimitiveArrayType : extends Type;
INJECT PrimitiveType : extends Type;

ReferenceType : 
   PrimitiveArrayType
   |
   ObjectType (=> "[" "]" )*
;

ObjectType# :
    <IDENTIFIER> [ TypeArguments ]
    ( "." <IDENTIFIER> =>|| [ TypeArguments ] )*
;

INJECT ObjectType : extends Type 
{
    public boolean equals(Object obj) {
        if (obj instanceof ObjectType) {
            return ((ObjectType) obj).toString().equals(this.toString());
        }
        return false;
    }
    
    public String toString() {
        StringBuilder buf = new StringBuilder();
        for (Token t : getRealTokens()) {
            buf.append(t);
        }
        return buf.toString();
    }
}

DiamondOperator :  "<" ">" =>|| ;

PossiblyEmptyTypeArguments #void :
   DiamondOperator
   |
   TypeArguments
;

TypeArguments : SCAN 2 "<" TypeArgument ( "," TypeArgument )* ">" ;

TypeArgument :
   (Annotation)*
   (
      ReferenceType
      |
      "?" [ WildcardBounds ]
   )
;

WildcardBounds : ("extends" | "super") ReferenceType ;

PrimitiveType# :
  "boolean" | "char" | "byte" | "short" | "int" | "long" | "float" | "double"
;

PrimitiveArrayType : PrimitiveType (=> "[" "]")+ ;

ReturnType# : "void" | Type ;

Name# : <IDENTIFIER> ( => "." <IDENTIFIER>)* ;

ThrowsList : "throws" Name ( "," Name )* ;

#Expression#abstract :
    AssignmentExpression
    {return (Expression) peekNode();}
;

INJECT Expression :
{
  public boolean isMethodCall() {return false;}
  public boolean isConstructorInvocation() {return false;}
  public boolean isAssignableTo() {return false;}
  public boolean isAllocation() {return false;}
}

INJECT AssignmentExpression : extends Expression
INJECT TernaryExpression : extends Expression 
INJECT ConditionalOrExpression : extends Expression 
INJECT ConditionalAndExpression : extends Expression 
INJECT InclusiveOrExpression : extends Expression 
INJECT InclusiveAndExpression : extends Expression 
INJECT ExclusiveOrExpression : extends Expression 
INJECT AndExpression : extends Expression 
INJECT EqualityExpression : extends Expression 
INJECT InstanceOfExpression : extends Expression 
INJECT RelationalExpression : extends Expression 
INJECT ShiftExpression : extends Expression
INJECT AdditiveExpression : extends Expression
INJECT MultiplicativeExpression : extends Expression
INJECT UnaryExpression : extends Expression
INJECT PreIncrementExpression : extends Expression
INJECT PreDecrementExpression : extends Expression
INJECT UnaryExpressionNotPlusMinus : extends Expression
INJECT PostfixExpression : extends Expression
INJECT PrimitiveCastExpression : extends Expression
INJECT ObjectCastExpression : extends Expression
INJECT PrimaryExpression : extends Expression
INJECT AllocationExpression : extends Expression 
INJECT LambdaExpression : extends Expression 
INJECT CodeBlock : extends Expression
INJECT SwitchExpression : extends Expression

AssignmentExpression :
  {
    Expression lhs;
  }
  TernaryExpression {lhs = (Expression) peekNode();}
  [
    SCAN 1 {lhs.isAssignableTo()}
    => AssignmentOperator Expression
  ]
;

AssignmentOperator #void :
  "=" | "*=" | "/=" | "%=" | "+=" | "-=" | "<<=" | ">>=" | ">>>=" | "&=" | "^=" | "|="
;

TernaryExpression :
  ConditionalOrExpression [  "?" Expression ":" Expression ]
;

ConditionalOrExpression :
  ConditionalAndExpression ( "||" ConditionalAndExpression )*
;

ConditionalAndExpression :
  InclusiveOrExpression ( "&&" InclusiveOrExpression )*
;

InclusiveOrExpression :
  ExclusiveOrExpression ( "|" ExclusiveOrExpression )*
;

ExclusiveOrExpression :
  AndExpression ( "^" AndExpression )*
;

AndExpression :
  EqualityExpression ( "&" EqualityExpression )*
;

EqualityExpression :
  InstanceOfExpression ( ( "==" | "!=" ) InstanceOfExpression )*
;

InstanceOfExpression :
  RelationalExpression 
  [ 
    "instanceof" Type 
    |
    LocalVariableDeclaration // This is a stable feature in JDK 16.
  ]
;

RelationalExpression :
  ShiftExpression 
  (
    ( 
     "<" 
     | ">" 
     | "<=" 
     | ">=" 
    ) 
    ShiftExpression
  )*
;


ShiftExpression :
   AdditiveExpression
   (
      ("<<" | ">>" | ">>>")
      AdditiveExpression
   )*
;

AdditiveExpression :
  MultiplicativeExpression (  ( "+" | "-" ) MultiplicativeExpression )*
;

MultiplicativeExpression :
  UnaryExpression (  ( "*" | "/" | "%" ) UnaryExpression )*
;

UnaryExpression :
  ( "+" | "-" ) UnaryExpression
  |
  PreIncrementExpression
  |
  PreDecrementExpression
  |
  UnaryExpressionNotPlusMinus
;

PreIncrementExpression : 
  "++" UnaryExpression
  [
    SCAN {currentLookaheadToken == null && !((Expression)peekNode()).isAssignableTo()}#
    => FAIL "expression after ++ is not an assignable variable"
  ]
;

PreDecrementExpression : 
  "--" UnaryExpression 
  [
    SCAN {currentLookaheadToken == null && !((Expression)peekNode()).isAssignableTo()}#
    => FAIL "expression after ++ is not an assignable variable"
  ]
;

UnaryExpressionNotPlusMinus :
  ( "~" | "!" ) UnaryExpression
  |
  LambdaExpression 
  |
  SwitchExpression
  |
  PrimitiveCastExpression 
  |
  ObjectCastExpression
  |
  PostfixExpression
;

PostfixExpression :
  PrimaryExpression 
  [ 
    SCAN 1 {currentLookaheadToken != null || ((Expression)peekNode()).isAssignableTo()}#
    =>
    "++" | "--" 
  ]
;

SwitchExpression# :
   SwitchStatement
;

PrimitiveCastExpression :
    "(" PrimitiveType ")" =>|| UnaryExpression
;

ObjectCastExpression :
   "(" ReferenceType ("&" ObjectType)* ")" =>|+1 UnaryExpressionNotPlusMinus
;

PrimaryExpression# :
    PrimaryPrefix 
    ( PrimarySuffix )*
;

MemberSelector :
  SCAN 3 "." TypeArguments <IDENTIFIER>
;

PrimaryPrefix#void :
  Literal
  |
  "this"
  |
  "super" "." =>|| [TypeArguments] <IDENTIFIER>
  |
  "(" Expression ")"
  |
  AllocationExpression 
  |
  => PrimitiveType "." "class"
  |
  => ReturnType "." "class"
  |
  MethodReference
  |  
  Name
;

INJECT PrimaryExpression :
{
  /**
   * Is this expression a method invocation?
   */
  public boolean isMethodCall() {
    if (!(getLastChild() instanceof InvocationArguments)) return false;
    if (getChild(0) instanceof Name && getChildCount() ==2) {
      return true;
    }
    Node secondLast = getChild(getChildCount()-2);
    Node thirdLast = getChildCount() == 2 ? null : getChild(getChildCount() -3);
    return (secondLast instanceof Identifier && thirdLast instanceof Token && ((Token)thirdLast).getType() == DOT)
           || secondLast instanceof MemberSelector;
  }

  public boolean isConstructorInvocation() {
    if (!(getLastChild() instanceof InvocationArguments)) return false;
    if (getChildCount() <2) return false;
    CONSTANTS_CLASS.TokenType tt = getChild(getChildCount()-2).getTokenType();
    return tt == SUPER || tt == THIS;
  }

  /**
   * If this expression is a method invocation, we 
   * return its name, otherwise null
   */
  public String getMethodName() {
    if (!isMethodCall()) {
      return null;
    }
    String fullName = getChild(getChildCount()-2).toString();
    return fullName.substring(fullName.lastIndexOf('.')+1);
  }

  public boolean isAllocation() {
    return getLastChild() instanceof AllocationExpression;
  }

  public boolean isAssignableTo() {
     if (getChildCount()==1) {
       return getChild(0) instanceof Name;
     }
     if (getChildCount() == 3 && getChild(0).getTokenType() == LPAREN && getLastChild().getTokenType() == RPAREN) {
       return ((Expression)getChild(1)).isAssignableTo();
     }
     Token lastToken = getLastToken();
     if (lastToken.getType() == IDENTIFIER) {
       Node secondLast = getChild(getChildCount() -2);
       return secondLast.getTokenType() == DOT;
     }
     if (lastToken.getType() == RBRACKET) {
       return getChild(getChildCount()-1).getTokenType() != LBRACKET;
     }
     return false;
  }
}


MethodReference :
    (Type | "super")
     "::" =>||
     (<IDENTIFIER>|"new")
;

PrimarySuffix#void :
 SCAN 2
 (
  => "." ("this"|"super")
  |
  "." =>|+1 AllocationExpression
  |
  MemberSelector
  |
  "[" [Expression] "]"
  |
  "." <IDENTIFIER>
  |
  "::" (<IDENTIFIER>|"new")
  |
  InvocationArguments
 )
;

Literal #void :
  <INTEGER_LITERAL>
  |
  <LONG_LITERAL>
  |
  <FLOATING_POINT_LITERAL>
  |
  <CHARACTER_LITERAL>
  |
  <STRING_LITERAL>
  |
  <TEXT_BLOCK_LITERAL>
  |
  "true"
  |
  "false"
  |
  "null"
;

InvocationArguments :
    "("
    [ Expression ("," Expression)* ]
    ")"
;

AllocationExpression :
  "new" PrimitiveType =>|| ArrayDimsAndInits
  |
  "new" ObjectType [ PossiblyEmptyTypeArguments ]
    (
      SCAN 2 ArrayDimsAndInits
      |
      InvocationArguments [ ClassOrInterfaceBody ]
    )
;

ArrayDimsAndInits :
  SCAN 2
  ( 
    "[" =>|+1 Expression "]" 
  )+ 
  ( 
    => "[" "]" 
  )*
  |
  ( 
    "[" "]" 
  )+ 
  ArrayInitializer
;

Label : <IDENTIFIER><COLON> =>|| ;

Statement  :
  (Label)*
  (
    AssertStatement
    |
    Block
    |
    YieldStatement
    |
    EmptyStatement
    |
    ExpressionStatement
    |
    SwitchStatement
    |
    IfStatement
    |
    WhileStatement
    |
    DoStatement
    |
    ForStatement
    |
    BreakStatement
    |
    ContinueStatement
    |
    ReturnStatement
    |
    ThrowStatement
    |
    SynchronizedStatement
    |
    TryStatement
  )
;

INJECT AssertStatement : extends Statement;
INJECT YieldStatement : extends Statement;
INJECT EmptyStatement : extends Statement;
INJECT SwitchStatement : extends Statement
INJECT IfStatement : extends Statement
INJECT WhileStatement : extends Statement
INJECT DoStatement : extends Statement
INJECT ExpressionStatement : extends Statement
INJECT ForStatement : extends Statement
INJECT BreakStatement : extends Statement
INJECT ContinueStatement : extends Statement
INJECT ReturnStatement : extends Statement
INJECT ThrowStatement : extends Statement
INJECT SynchronizedStatement : extends Statement
INJECT TryStatement : extends Statement
INJECT TryWithResources : extends TryStatement
INJECT ClassicTryStatement : extends TryStatement

TryStatement #(false) :
   TryWithResources
   |
   ClassicTryStatement
   |
   "try" FAIL "Expecting '{' or '(' after 'try'"
;

AssertStatement : "assert" Expression [ ":" Expression ] ";" ;

Block #CodeBlock : "{" (BlockStatement)*! "}" ;

BlockStatement :
// The YieldStatement option in the Statement production will thus never
// be matched but this doesn't seem to be a problem really.
// We need YieldStatement up top here so it does not spuriously match LocalVariableDeclaration.
  YieldStatement 
  |
  => LocalVariableDeclaration ";"
  |
  ClassDeclaration
  |
  InterfaceDeclaration
  |
  Statement
;

LocalVariableDeclaration : 
  Modifiers(EnumSet.of(FINAL)) Type =>|+1 VariableDeclarator ( "," VariableDeclarator )* 
;

EmptyStatement : ";" ;

StatementExpression :
  PreIncrementExpression
  |
  PreDecrementExpression
  |
  PrimaryExpression 
  {Expression lhs = (Expression) peekNode();}
  (
      SCAN 1 {lhs.isAssignableTo()}=>
      (
          ("++" | "--") #PostfixExpression(2)
          |
          AssignmentOperator Expression #AssignmentExpression(3)
      )
      |
      SCAN 0 {lhs.isMethodCall() || lhs.isConstructorInvocation() || lhs.isAllocation()}
      =>{}
      |
      FAIL "Not a valid statement"
  )
;

ExpressionStatement : StatementExpression ";" ;

SwitchStatement :
  SCAN "switch" "(" Expression ")" "{" NewSwitchLabel
  => NewSwitchStatement
  |
  ClassicSwitchStatement
;

ClassicSwitchStatement : 
  "switch" "(" Expression ")" "{"
  (ClassicCaseStatement)*
  "}"    
;

ClassicSwitchLabel :
  (
   "case" Expression ("," Expression)* 
   |
   "default" 
  )
  ":"
;

ClassicCaseStatement :
  ClassicSwitchLabel
  (SCAN 1 {getToken(1).getType() != _DEFAULT} 
          // We need this because we'll spuriously enter the BlockStatement 
          // if the next token is "default", because "default" is in the 
          // BlockStatement first set. This is really quite tricky, but maybe not my
          // fault, but the Java designers for re-using keywords!
  => BlockStatement)*
;

NewSwitchStatement :
  "switch" "(" Expression ")" "{"
  (NewCaseStatement)*
  "}"
;

NewSwitchLabel :
  (
   "case" Expression ("," Expression)* 
   |
   "default" 
  )
  "->"
;

NewCaseStatement : 
   NewSwitchLabel 
   (
     Block
     |
     ThrowStatement
     |
     SCAN \.\.\SwitchExpression
     => Expression ";"
     |
     SCAN ~\.\.\SwitchExpression
     => BlockStatement
   )
;

IfStatement :
  "if" "(" Expression ")" Statement [ "else" Statement ]
;

WhileStatement :
  "while" "(" Expression ")" Statement
;

DoStatement :
  "do" Statement "while" "(" Expression ")" ";"
;

ForStatement :
  "for" "("
  (
      Modifiers(EnumSet.of(FINAL)) Type <IDENTIFIER> ":" =>|| Expression 
      |
      [ ForInit ] ";" [ Expression ] ";" [ StatementExpressionList ]
  )
  ")" Statement
;

ForInit :
  LocalVariableDeclaration
  |
  StatementExpressionList
;

StatementExpressionList :
  StatementExpression ( "," StatementExpression )*
;

BreakStatement :
  "break" [ <IDENTIFIER> ] ";"
;

YieldStatement :
   SCAN {getToken(1).getImage().equals("yield") 
        && isInProduction("SwitchExpression")}# 
   => <IDENTIFIER> Expression ";" 
;

ContinueStatement : "continue" [ <IDENTIFIER> ] ";" ;

ReturnStatement : "return" (Expression)? ";" ;

ThrowStatement : "throw" Expression ";" ;

SynchronizedStatement : "synchronized" "(" Expression ")" Block ;

ClassicTryStatement :
  "try"
  =>|+1
  Block 
  (
     FinallyBlock
     |
     (
        (CatchBlock)+ 
        [FinallyBlock]
     )
  )
;

TryWithResources :
   "try" "(" =>||
    (
         => LocalVariableDeclaration
         |
         Name
    )
    (
        ";"
       [
          => LocalVariableDeclaration
          |
          Name
       ]
    )*
    ")"
    Block
    (CatchBlock)*
    [FinallyBlock]
;

CatchBlock :
    "catch"
    "("
    ["final"]
    ObjectType ("|" ObjectType)* VariableDeclaratorId
    ")"
    Block 
;

INJECT CatchBlock : extends Statement
{
    public CodeBlock getBlock() {
        return firstChildOfType(CodeBlock.class);
    }
}

FinallyBlock : "finally" Block;

INJECT FinallyBlock :
{
    public CodeBlock getBlock() {
        return firstChildOfType(CodeBlock.class);
    }
}

Annotation #Annotation :
 SCAN 2
 ( 
   NormalAnnotation
   |
   SingleMemberAnnotation
   |
   MarkerAnnotation
 )
;

NormalAnnotation #void : "@" Name "(" =>|+2 [ MemberValuePairs ] ")" ;

MarkerAnnotation #void : "@" Name ;

SingleMemberAnnotation #void :
  "@" Name "(" =>|| MemberValue ")"
;

INJECT Annotation : 
{
  public String getName() {
    return getChild(1).toString();
  }
}

MemberValuePairs :
   MemberValuePair ( "," MemberValuePair )*
;

MemberValuePair :  <IDENTIFIER> "=" MemberValue;

MemberValue :
   Annotation
   |
   MemberValueArrayInitializer
   | 
   TernaryExpression
;

MemberValueArrayInitializer :
  => "{" "}"
  |
  "{" MemberValue ( "," =>|+1 MemberValue )* [ "," ] "}"
;


AnnotationTypeDeclaration :
  Modifiers(EnumSet.of(PUBLIC, PROTECTED, PRIVATE, ABSTRACT, STATIC, STRICTFP))
  "@" "interface" =>|| <IDENTIFIER> AnnotationTypeBody
;

AnnotationTypeBody :
  "{" ( AnnotationTypeMemberDeclaration )* "}"
;

AnnotationMethodDeclaration :
   Modifiers(EnumSet.of(PUBLIC,ABSTRACT))
   Type <IDENTIFIER> "(" =>|| ")" [ DefaultValue ] ";"
;

AnnotationTypeMemberDeclaration :
  EmptyDeclaration 
  |
  AnnotationTypeDeclaration
  |
  ClassDeclaration
  |
  InterfaceDeclaration
  |
  EnumDeclaration
  |
  AnnotationMethodDeclaration
  |
  FieldDeclaration
;

DefaultValue : "default" MemberValue ;

LambdaExpression : 
  SCAN ~\...\NewSwitchLabel =>
  LambdaLHS =>|| (Expression | Block) 
;

LambdaLHS : (<IDENTIFIER> | SimpleLambdaLHS | FormalParameters) "->" =>|| ;

SimpleLambdaLHS #void :
   SCAN 3
   "(" <IDENTIFIER> 
   (","<IDENTIFIER>)*
   ")"
;

